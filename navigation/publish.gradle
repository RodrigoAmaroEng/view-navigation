apply plugin: "maven-publish"

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id project.findProperty("dev.id")
            name project.findProperty("dev.name")
            email project.findProperty("dev.email")
        }
    }

    scm {
        url project.findProperty("pom.scm")
    }
}

publishing {
    repositories {
        maven {
            name = project.findProperty("git.bucket")
            url = uri("https://maven.pkg.github.com/${project.findProperty("dev.id")}/${project.findProperty("git.bucket")}")
            credentials {
                username = project.findProperty("github.user") ?: System.getenv("USERNAME")
                password = project.findProperty("github.key") ?: System.getenv("TOKEN")
            }
        }
    }
    project.afterEvaluate {
        publications {
            mavenJava(MavenPublication) {
                artifact bundleReleaseAar
                groupId project.findProperty("pom.group")
                artifactId project.findProperty("pom.artifact")
                version project.findProperty("pom.version")
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', project.findProperty("pom.description"))
                    root.appendNode('name', project.findProperty("pom.name"))
                    root.appendNode('url', project.findProperty("pom.scm"))
                    root.children().last() + pomConfig
                }
            }
        }
    }
}

task pomVersion() {
    println(project.findProperty("pom.version"))
}